export func ping(player: Entity) {
    var targets = player.scene.findByTag(["echo"]);
    var sfx = @Sound("ping");
    sfx.play();

    for(target, targets) {
        task {
            var position = (target.worldPosition - @Camera.getPosition());
            var roundTrip = position.length() * 2f;

            if(roundTrip > 1000f) {
                die;
            }
            sleep(roundTrip);

            //target.isVisible = true;

            var bus = @AudioBus();
            var spacializer = @AudioSpacializer();
            var delay = @AudioDelay();
            var reverb = @AudioReverb(roundTrip / 4f, roundTrip / 200f);
            var lowPass = @AudioLowPassFilter();

            bus.addEffect(spacializer);
            bus.addEffect(delay);
            bus.addEffect(reverb);
            bus.addEffect(lowPass);

            var angle_ = 0f;
            var itd = 0.002f;

            spacializer.position = position.rotated(angle_);
            var d = position.normalized.dot(@Vec2f(1f, 0f).rotated(angle_));

            if (d < 0f) {
                delay.leftDelay = 0f;
                delay.rightDelay = -itd * d;
                lowPass.leftDamping = 0f;
                lowPass.rightDamping = -d * 0.9f;
            }
            else if (d > 0f) {
                delay.rightDelay = 0f;
                delay.leftDelay = itd * d;
                lowPass.leftDamping = d * 0.9f;
                lowPass.rightDamping = 0f;
            }
            else {
                delay.leftDelay = 0f;
                delay.rightDelay = 0f;
                lowPass.leftDamping = 0f;
                lowPass.rightDamping = 0f;
            }

            bus.play(sfx);
            sleep(roundTrip * 5f);
            //target.isVisible = false;
            bus.disconnect();
    }();
    }
}